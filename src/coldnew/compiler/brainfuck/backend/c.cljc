(ns coldnew.compiler.brainfuck.backend.c
  (:require [clojure.pprint :refer [cl-format]]
            [clojure.string :as str]))

(declare generate-runtime ir->code)

(defn ir->c
  ([ir] (ir->c ir 3000))
  ([ir num-cells]
   (generate-runtime num-cells (apply str (map ir->code ir)))))

(defn generate-runtime
  [num-cells body]
  (cl-format nil "// This file is generated by coldnew's brainfuck compiler
// You can use following command to compile it:
//
//    gcc xxx.c -o xxx
//

#include <stdio.h>

int main(int argc, char *argv[])
{
\t// num-cells
\tchar cells[~d] = {0};
\tchar *ptr = cells;

\t// body
~d

\treturn 0;
}
" num-cells body))


(defmulti ir->code (fn [ir] (:op ir)))

(defmethod ir->code :default
  [ir]
  (throw (ex-info "Unknown IR found " {:ir ir})))

;;;; Basic IR

(defmethod ir->code :add
  [ir]
  (let [indent (apply str (repeat (:indent-depth ir) "\t"))]
    (str indent "++*ptr;" "\n")))

(defmethod ir->code :sub
  [ir]
  (let [indent (apply str (repeat (:indent-depth ir) "\t"))]
    (str indent "--*ptr;" "\n")))

(defmethod ir->code :right
  [ir]
  (let [indent (apply str (repeat (:indent-depth ir) "\t"))]
    (str indent "++ptr;" "\n")))

(defmethod ir->code :left
  [ir]
  (let [indent (apply str (repeat (:indent-depth ir) "\t"))]
    (str indent "--ptr;" "\n")))

(defmethod ir->code :output
  [ir]
  (let [indent (apply str (repeat (:indent-depth ir) "\t"))]
    (str indent "putchar(*ptr);" "\n")))

(defmethod ir->code :input
  [ir]
  (let [indent (apply str (repeat (:indent-depth ir) "\t"))]
    (str indent "*ptr = getchar();" "\n")))

(defmethod ir->code :loop
  [ir]
  (let [indent (apply str (repeat (:indent-depth ir) "\t"))]
    (str indent "while (*ptr) {" "\n"
         indent (str/join indent (map ir->code (:children ir)))
         indent "}" "\n")))

;;;; Extended IR (for optimize)

(defmethod ir->code :set-cell-value
  [ir]
  (let [indent (apply str (repeat (:indent-depth ir) "\t"))]
    (str indent "*ptr += " (:val ir) ";\n")))

(defmethod ir->code :set-cell-pointer
  [ir]
  (let [indent (apply str (repeat (:indent-depth ir) "\t"))]
    (str indent "ptr += " (:val ir) ";\n")))

(defmethod ir->code :clear
  [ir]
  (let [indent (apply str (repeat (:indent-depth ir) "\t"))]
    (str indent "*ptr = 0;" "\n")))
