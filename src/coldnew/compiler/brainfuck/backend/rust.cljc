(ns coldnew.compiler.brainfuck.backend.rust
  (:require [clojure.pprint :refer [cl-format]]
            [clojure.string :as str]
            [coldnew.compiler.brainfuck.utils :refer [line-indent]]))

(declare generate-runtime ir->code)

(defn ir->rust
  ([ir] (ir->rust ir 3000))
  ([ir num-cells]
   (generate-runtime num-cells (apply str (map ir->code ir)))))

(defn generate-runtime
  [num-cells body]
  (cl-format nil "// This file is generated by coldnew's brainfuck compiler
// You can use following command to compile it:
//
//    rustc xxx.rs -o xxx
//

#[allow(unused_imports)]
use std::io::Read;
#[allow(unused_imports)]
use std::io::stdin;

fn main() {
\tlet mut cells :Vec<u32> = vec![0; ~d];
\tlet mut ptr :usize = 0;

~d

}
" num-cells body))

(defmulti ir->code (fn [ir] (:op ir)))

(defmethod ir->code :default
  [ir]
  (throw (ex-info "Unknown IR found " {:ir ir})))

;;;; Basic IR

(defmethod ir->code :add
  [ir]
  (line-indent ir "cells[ptr] += 1;"))

(defmethod ir->code :sub
  [ir]
  (line-indent ir "cells[ptr] -= 1;"))

(defmethod ir->code :right
  [ir]
  (line-indent ir "ptr += 1;"))

(defmethod ir->code :left
  [ir]
  (line-indent ir "ptr -= 1;"))

(defmethod ir->code :output
  [ir]
  (line-indent ir "print!(\"{}\", cells[ptr] as u8 as char);"))

(defmethod ir->code :input
  [ir]
  (str (line-indent ir "let mut buf :Vec<u8> = vec![0];")
       (line-indent ir "stdin().read(&mut buf).ok().expect(\"Failed to read from stdin.\");")
       (line-indent ir "cells[ptr] = buf[0];")))

(defmethod ir->code :loop
  [ir]
  (str (line-indent ir  "while cells[ptr] != 0 {")
       (str/join "" (map ir->code (:children ir)))
       (line-indent ir "}")))

;;;; Extended IR (for optimize)

(defmethod ir->code :set-cell-value
  [ir]
  (line-indent ir "cells[ptr]" (if (< (:val ir) 0)
                                 (str " -= " (- (:val ir)))
                                 (str " += " (:val ir)))
               ";"))

(defmethod ir->code :set-cell-pointer
  [ir]
  (line-indent ir "ptr" (if (< (:val ir) 0)
                          (str " -= " (- (:val ir)))
                          (str " += " (:val ir)))
               ";"))

(defmethod ir->code :clear
  [ir]
  (line-indent ir "cells[ptr] = 0;"))